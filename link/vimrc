" vim: set foldmarker={,} foldlevel=0 foldmethod=marker spell:
" Author: Kyle Huang (https://github.com/kal444)
"
" Based on many others' vimrc files
"   many years ago, the initial version is http://www.guckes.net/vim/
"   then, it went through many modifications
"   recently, I'm inspired to look at more vim plugins and started to go
"   through this: https://github.com/square/maximum-awesome
"   some more 'sensible' settings from https://github.com/tpope/vim-sensible
"   recent additions from https://github.com/spf13/spf13-vim/blob/3.0/.vimrc
"    * really like the folding idea

" Identify platform {
silent function! OSX()
  return has('macunix')
endfunction
silent function! LINUX()
  return has('unix') && !has('macunix') && !has('win32unix')
endfunction
silent function! WINDOWS()
  return  (has('win32') || has('win64'))
endfunction
" }

" Set VIMRC location and runtime path {
" vimrc location can vary based on OSes
" see :version
" http://vim.wikia.com/wiki/Open_vimrc_file#Location_of_vimrc
"========== save vimrc location for later use
if WINDOWS()
  let vimrc='$HOME\_vimrc'
  " keep the same .vim name instead of vimfiles
  set runtimepath=$HOME/.vim,$VIM/vimfiles,$VIMRUNTIME,$VIM/vimfiles/after,$HOME/.vim/after
else
  let vimrc='$HOME/.vimrc'
endif
" }

" Configure Vundle {
" This expects Vundle is already cloned into bundles directory
filetype off
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()
" Vundle bundles
if filereadable(expand("~/.vim/vimrc.bundles"))
  source ~/.vim/vimrc.bundles
  source ~/.vim/vimrc.bundles.local
endif
call vundle#end()
" }

" Settings {
set nocompatible " we want VIM not VI!

"========== File/buffer handling
set autoread " read the file again when it's changed outside vim and not inside vim
set autowrite " write file when :next, :rewind, :last, :first, :previous, :stop, :suspend, :tag, :!, :make commands are used
set nobackup " backups are for wimps  ;-)
set backupcopy=yes " see :help crontab
set encoding=utf-8
set hidden " keep the changed buffer without saving it
set writebackup " make a backup before overwriting a file. then remove it after the file was successfully written

"========== Tabs and spaces
set autoindent " copy indent from current line when starting a new line
set expandtab " use only spaces instead of tabs
set shiftwidth=2 " number of spaces to use for each step of (auto)indent
set smarttab " smart handling of tab at the front of the line vs in the line
set softtabstop=2 " number of spaces to use for each <Tab> or <BS> in editing
set tabstop=8 " real tabs are 8 spaces

"========== Folding
set foldenable " folding is good
set foldmethod=indent " make folding indent sensitive
set foldlevel=10 " don't start folding too early

"========== Display
set background=dark " use the dark bg color set
set cursorline " makes cursor easier to find
set nocursorcolumn " don't make cursor TOO easy to find - too distracting
set display+=lastline " display last line as much as possible
set laststatus=2 " always show status line
set lazyredraw " don't update screen while executing macros, registers and other commands that have not been typed
set list " show trailing whitespace
set listchars=tab:▸\ ,trail:▫ " set nicer chars for tabs and trailing spaces
set modeline " do use modelines
set number " do show line numbers
set report=0 " report all changed lines
set ruler " show cursor position always
set scrolloff=1 " show 1 row context above/below cursorline
set sidescrolloff=5 " show 5 columns as context
set shortmess+=atI " abbreviate all messages
set showcmd " show current uncompleted command
set showmatch " show matching bracket briefly
set showmode " show the current mode
"my status line
set statusline=%F%m%r%h%w\ [FORMAT=%{&ff}]\ [TYPE=%Y]\ [%p%%][POS=%l,%v][LINES=%L]
set tabpagemax=50 " limit tabs opened
set title " have vim try to update the title of the window
set ttimeout ttimeoutlen=100 " timeout keycode after 100ms, do not timeout mappings
set ttyfast " we are on a fast connection
set wildchar=<TAB> " use tab to start wildcard expansion in the command-line
set wildignore=log/**,node_modules/**,target/**,tmp/**,*.rbc " ignore some files/directories for completion
set wildmenu " show a navigable menu for tab completion
set wildmode=longest,list,full " longest match, and list if can't match only one

set noerrorbells " disable error bell beep
if WINDOWS()
  set novisualbell
else
  set visualbell " use visual bell, but we will disable it later
  set t_vb= " disable visualbell terminal chars
endif

"========== Editting
set backspace=indent,eol,start " this is much smarter. back over autoindent,<EOL> and insert position
set clipboard=unnamed " yank and paste with the system clipboard
set esckeys " allow cursor movement in insert mode
set formatoptions+=2j " better indenting. join comment lines better
set nrformats-=octal " octal would cause numbers with leading zero to be treated as octal. Not good with ^A and ^X
set virtualedit=all " allow cursor movement anywhere
set whichwrap+=<,>,[,] " allow left, right keys to wrap in normal/visual mode

"========== History and Searching
set history=1000 " keep 1000 commands and 1000 search patterns in the history.
set hlsearch " highlight searches
set incsearch " display the match for a search pattern when halfway typing it
set ignorecase " do ignore case in search patterns. This works well with smartcase set on
set smartcase " ignore case when the pattern contains lowercase letters only
if executable('ag')
  " Use ag over grep
  set grepprg=ag\ --nogroup\ --nocolor
endif

" Enable basic mouse behavior such as resizing buffers.
set mouse=a
if exists('$TMUX')  " Support resizing in tmux
  set ttymouse=xterm2
endif
" }

" Leader mapping {
let mapleader = ','
let g:mapleader = ','
let maplocalleader = ','
let g:maplocalleader = ','
" }

" Plugin settings {

" lightline config {
let g:lightline = {
  \ 'colorscheme': 'solarized',
  \ 'active': {
  \   'left': [ [ 'mode', 'paste' ],
  \             [ 'fugitive', 'readonly', 'filename', 'modified' ] ]
  \ },
  \ 'component': {
  \   'fugitive': '%{exists("*fugitive#head")?fugitive#head():""}'
  \ },
  \ 'component_visible_condition': {
  \   'fugitive': '(exists("*fugitive#head") && ""!=fugitive#head())'
  \ },
\ }
" }

" indent guides config {
let g:indent_guides_enable_on_vim_startup = 1
let g:indent_guides_auto_colors = 1
let g:indent_guides_guide_size = 0
let g:indent_guides_default_mapping = 0

nmap <leader>ig <Plug>IndentGuidesToggle
" }

" gitgutter config {
let g:gitgutter_enabled = 1 " do enable git gutter
let g:gitgutter_realtime = 0 " don't update in realtime
let g:gitgutter_eager = 0 " don't update eagerly, wait for file save
let g:gitgutter_map_keys = 0 " don't map keys automatically

nmap [c <Plug>GitGutterPrevHunk
nmap ]c <Plug>GitGutterNextHunk
nmap <leader>hs <Plug>GitGutterStageHunk
nmap <leader>hu <Plug>GitGutterUndoHunk
nnoremap <leader>gg :GitGutterToggle<cr>
" }

" NERDTree config {
nnoremap <leader>nt :NERDTreeToggle<cr>
nnoremap <leader>ntf :NERDTreeFind<cr>
" }

" tagbar config {
nnoremap <leader>tb :TagbarToggle<cr>
" }

" tabular config {
nnoremap <leader>l :Tabularize<space>/
vnoremap <leader>l :Tabularize<space>/
" }

" delimitMate config {
let delimitMate_autoclose = 1
let delimitMate_expand_cr = 1
let delimitMate_expand_space = 1
" }

" matchit config {
" Load matchit.vim, but only if the user hasn't installed a newer version.
if !exists('g:loaded_matchit') && findfile('plugin/matchit.vim', &rtp) ==# ''
  runtime! macros/matchit.vim
endif
" }

" expand region config {
" Extend the expansion text objects
call expand_region#custom_text_objects({
      \ 'a]'  :1,
      \ 'ab'  :1,
      \ 'aB'  :1,
      \ 'ii'  :0,
      \ 'ai'  :0,
\ })

map + <Plug>(expand_region_expand)
map _ <Plug>(expand_region_shrink)
" }

" ack config {
if executable('ag')
  " Use Ag over ack
  let g:ackprg = 'ag --vimgrep'
  cnoreabbrev ag Ack
  cnoreabbrev Ag Ack
endif
" }

" ctrl-p config {
let g:ctrlp_match_window = 'order:ttb,max:15' " change CtrlP search result order and size

if executable('ag')
  " Use ag in CtrlP. Respects .gitignore, but show hidden files
  let g:ctrlp_user_command = 'ag %s -l -g "" --ignore "\.git$\|\.hg$\|\.svn$"'
endif

nmap <c-p> <Plug>(ctrlp)
nnoremap <leader>b :CtrlPBuffer<cr>
nnoremap <leader>t :CtrlP<cr>
nnoremap <leader>T :CtrlPClearCache<cr>:CtrlP<cr>
" }

" clever-f config {
let g:clever_f_across_no_line = 1
let g:clever_f_smart_case = 1
" }

" easymotion config {
let g:EasyMotion_do_mapping = 0
let g:EasyMotion_smartcase = 1
let g:EasyMotion_use_smartsign_us = 1

map <leader><leader> <Plug>(easymotion-prefix)
map <leader>j <Plug>(easymotion-j)
map <leader>k <Plug>(easymotion-k)
nmap s <Plug>(easymotion-overwin-f2)
" }

" neocomplete & neosnippet config {
" start completion always
let g:neocomplete#enable_at_startup = 1
let g:neocomplete#enable_auto_select = 0
let g:neocomplete#enable_smart_case = 1
" use vim-snippets
let g:neosnippet#enable_snipmate_compatibility = 1
let g:neosnippet#snippets_directory='$HOME/.vim/bundle/vim-snippets/snippets'

" <C-h>, <BS>: close popup and delete backward char.
inoremap <expr> <BS> neocomplete#smart_close_popup()."\<c-h>"
inoremap <expr> <c-h> neocomplete#smart_close_popup()."\<c-h>"

" <CR>: close popup and save indent. This does not insert CR after selection
inoremap <silent> <CR> <c-r>=<SID>neo_cr_function()<CR>
function! s:neo_cr_function()
  return pumvisible() ? "\<C-y>" : "\<CR>"
endfunction

" use tab to navigate popup. expand/jump when there is no popup
imap <expr> <TAB>
 \ pumvisible() ?
 \   "\<c-n>" :
 \   neosnippet#expandable_or_jumpable() ?
 \     "\<Plug>(neosnippet_expand_or_jump)" :
 \     "\<TAB>"

" when you fill out placeholders for snippets, vim is in select mode
" this allows you to cycle through the placeholder fields using tab
smap <expr> <TAB>
 \ neosnippet#expandable_or_jumpable() ?
 \   "\<Plug>(neosnippet_expand_or_jump)" :
 \   "\<TAB>"
" }

" }

" Key mappings {

" jj quickly in insertmode gives escape key
inoremap jj <esc>
" n / N are updated to always center (vertically) the matches
nnoremap n nzzzv
nnoremap N Nzzzv
" p in visual mode yank into blackhole register before pasting
vnoremap p "_dP
" Q quits - normally Q opens ex-mode, never uses it
nnoremap Q :q<cr>
" U redos - normally U undo a whole line, never uses it
nnoremap U :redo<cr>
" rewrite the file with sudo
cnoremap w!! w !sudo tee > /dev/null %
" >, <: reselect visual block after indent
xnoremap > >gv
xnoremap < <gv
" CTRL-A normal mode: increment number
" CTRL-X normal mode: decrement number
" insert mode ^u will clear line, but undo buffer now saves the cleared text.
inoremap <c-u> <c-g>u<c-u>
" insert mode ^w will clear word, but undo buffer now saves the cleared text.
inoremap <c-w> <c-g>u<c-w>
" <cr>: highlight visual selections
xnoremap <cr> y:let @/ = @"<cr>:set hlsearch<cr>

" leader key bindings in alpha order
nnoremap <leader><space> :call whitespace#strip_trailing()<cr>
" toggle paste mode
nnoremap <leader>1 :set paste!<cr>
" cd: Switch to the directory of the open buffer
nnoremap <leader>cd :cd %:p:h<cr>:pwd<cr>
" cel = clear empty lines - deletes all empty or whitespace only lines
vnoremap <leader>cel :g/^[<c-i> ]*$/d<cr>
" clear hlsearch highlights
nnoremap <leader>hl :let @/ = ""<cr>
" quit all - useful when viewing vimdiff
nnoremap <leader>q :qall<cr>
" ri = re-indent whole file and put you back to where you were
" a bit magic here: save current location, save top of window loc,
" reformat gg=G, move back to original top of windows, and then last line
nnoremap <leader>ri mqHmwgg=G`wzt`q
" rcm = remove ^M from DOS file
nnoremap <leader>rcm :%s/<c-v><c-m>//g<cr>
" vimrc editing (edit this file)
nnoremap <leader>v :exe ":edit " . vimrc<cr>
" reload vimrc by reading this file
nnoremap <leader>V :exe ":source " . vimrc<cr>

" window movements
nnoremap <c-h> <c-w>h
nnoremap <c-j> <c-w>j
nnoremap <c-k> <c-w>k
nnoremap <c-l> <c-w>l
" switch to the other window ala ^w^w, but maximize the target window too
nnoremap <c-w><c-e> <c-w><c-w><c-w>_<cr>
" <leader>m: Maximize current split
nnoremap <leader>m <c-w>_<c-w><bar>

" }

" GUI configuration {
if has("gui_running")
  set columns=132 " default to 132 columns
  set lines=50 " default to 50 lines

  set guicursor=a:blinkon0 " diable blinky cursor
  set mousehide " hide the mouse when typing

  " at some point the fonts will be needed
  if OSX()
    set guifont=Bitstream\ Vera\ Sans\ Mono:h11,Menlo\ Regular:h11,Andale\ Mono:h11
  elseif WINDOWS()
    set guifont=Bitstream_Vera_Sans_Mono:h10,DejaVu_Sans_Mono:h10,Consolas:h10
  else
    set guifont=Bitstream\ Vera\ Sans\ Mono\ 10,DejaVu\ Sans\ Mono\ 10,Consolas\ 10
  endif
endif
" }

filetype plugin indent on " enable file type detection.
syntax on " turn on syntax highlighting
colorscheme solarized

" enable omni completion.
autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags
autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
autocmd FileType ruby setlocal omnifunc=rubycomplete#Complete
autocmd FileType haskell setlocal omnifunc=necoghc#omnifunc

" set extensions for markdown files
autocmd BufNewFile,BufRead *.markdown,*.mkd,*.md set filetype=markdown
" markdown files should have spell checking
autocmd BufNewFile,BufRead *.markdown,*.mkd,*.md set spell
" restore last edit position if we can
autocmd BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$") | exe "normal! g`\"" | endif
" automatically rebalance windows on vim resize
autocmd VimResized * :wincmd =

if filereadable(expand("~/.vim/vimrc.local"))
  source ~/.vim/vimrc.local
endif
